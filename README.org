#+title: Gnuplot
#+author: Isaías Moreno-Cruz
#+OPTIONS: toc:nil num:nil html-style:nil

* Introducción

[[http://www.gnuplot.info/][Gnuplot]] es un programa de línea de comandos que te permite realizar gráficas de una calidad impecable, las cuales tienen la calidad necesaria para ser publicadas en cualquier tipo de revista. Aunque Gnuplot pudiera parecer un programa complicado al inicio, es todo lo contrario, un programa muy amigable con la configuración correcta.

En general Gnuplot te permite graficar un conjunto de datos 

Es un curso para usuarios de linux.

* Instalación

Para instalar Gnuplot desde la linea de comandos:

#+begin_src sh :exports both
## Debbian/Ubuntu
sudo apt-get install gnuplot
#+end_src

#+begin_src sh :exports both
## Archlinux
sudo pacman -S gnuplot  
#+end_src

Para macOS puede instalar [[https://brew.sh/][Homebrew]] (necesita Xcode), en Windows puede ser una alternativa Windows Subsystem for Linux 2 (WSL2).


También es necesario tener instalado LaTeX
#+begin_src sh :exports both
  ## Archlinux
  sudo pacman -S texlive
#+end_src



** Ejecución

#+begin_src sh :none :exports both
archlinux➜   ᐅ  gnuplot

        G N U P L O T
        Version 6.0 patchlevel 1    last modified 2024-05-13 

        Copyright (C) 1986-1993, 1998, 2004, 2007-2024
        Thomas Williams, Colin Kelley and many others

        gnuplot home:     http://www.gnuplot.info
        faq, bugs, etc:   type "help FAQ"
        immediate help:   type "help"  (plot window: hit 'h')

        Terminal type is now qt
gnuplot> 
#+end_src



* Primera gráfica

Para iniciar vamos a utilizar los datos de un archivo llamado "junio.dat", es un archivo separados por /tabs/, que tiene dos columnas: cenit, acimut.

#+begin_src shell :results output :exports both
  head ./data/junio.dat
#+end_src

#+RESULTS:
#+begin_example
cenit	acimut
66.5503	-104.795100
66.3393	-104.698000
65.2827	-104.213800
64.2238	-103.731600
63.1626	-103.251000
62.0992	-102.771700
61.0337	-102.293500
59.9662	-101.816000
58.8967	-101.338900
#+end_example

Para graficar el archivo en gnuplot únicamente escribimos, previamente hay que ejecutar =gnuplot=, el siguiente comando: 

#+begin_src gnuplot :file "./fig/primera.png" :results file graphics :exports both
  reset
  file="./data/junio.dat"
  plot file using 2:1 with lines
#+end_src

#+RESULTS:
[[file:./fig/primera.png]]

Después de =plot= se indica entre comillas el nombre del archivo o la ruta, en este caso ="./data/junio.dat"= , seguido se indica las columnas que se utilizarán para realizar la gráfica =using 2:1= 2 para el eje de las abscisas y 1 para el eje de las ordenadas. Por último se indica como se dibujaran los puntos, en este caso con una linea =with lines=.

** Titulos y etiquetas 

Habiamos graficado la columna dos (acimut) Vs la uno (cenit), no es común gráficar el cenit sino su ángulo complementario, $\alpha$ por lo que es necesario hacer una operación sobre la columna dos (90-cenit) lo cual es posible hacer en gnuplot. Adicionalmente también es posible agregar etiquetas a los ejes, así como un titulo.

#+begin_src gnuplot :file "./fig/primera2.png" :results file graphics :exports both :session primera 
  reset
  set xlabel 'gamma'
  set ylabel 'alpha'
  set yrange [0, 90]
  set grid
  file="./data/junio.dat"
  plot file using 2:(90-$1) with lines title "Junio"
#+end_src

#+RESULTS:
[[file:./fig/primera2.png]]

** PNG

La imagen obtenida puede ser guardada en una imagen png, con el siguiente comando:

#+begin_src gnuplot :results none :exports both :session primera 
  set terminal png size 600,500 enhanced font "Helvetica,20"
  set output './fig/junio.png'
  replot
#+end_src

Ahora tenemos una gráfica en un archivo png que no tiene la calidad que nosotros deseamos, por lo que en lugar de exportarlo en /png/, vamos a exportarlo en formato /tex/ de /LaTeX/. Pero antes es necesario  hablar de los script de gnuplo.

* Script de Gnuplot

Escribir directamente en la terminal es tedioso, es mejor utilizar un archivo de texto plano en el cual vamos a escribir todas las lineas de comando antes indicadas. La diferencia es que ahora será posible modificar el archivo según sea nuestro interes. Posteriormente los ejecutamos en gnuplot.

Scritp  de Gnuplot. Las extensiones que puede tener el archivo son /.gnuplot/ o /.gp/. En mi caso utilizo una extensión /gp/ para identificar los script de gnuplot. A continuación se muestra el contenido del archivo =junio.gp=. 


#+begin_src gnuplot :results none :exports both  :tangle junio.gp
  reset
  set xlabel '$\gamma$'
  set ylabel '$\alpha$'
  set yrange [0:90]
  set grid
  file="./data/junio.dat"
  plot file using 2:(90-$1) with lines title "Junio"
#+end_src

Ahora podemos entrar a /gnuplot/ y leer el script con el siguiente comando:

#+begin_src gnuplot :file "./fig/junioS.png" :results file graphics :exports both
load 'junio.gp'
#+end_src

#+RESULTS:
[[file:./fig/junioS.png]]



** LaTeX

En lugar de exportar la gráfica a un archivo png es posible exportarlo a un archivo LaTeX. A continuación vamos a gráficar 

#+begin_src gnuplot :results none :exports both
  load 'junio.gp'
  set terminal epsl col sol lw 2
  set output 'junio.tex'
  replot
#+end_src

Ahora tenemos un archivo /junio.eps/ y un archivo /junio.tex/, para obtener la grafica deseada es necesario crear un pequeño archivo de latex que nos permita compilar ...


Ahora tenemos un archivo /tex/ con la gráfica. Esta información ya es posible agregar a un documento de LaTeX, para que nosotros puedamos visualizarla vamos a escribir un prqueño texto de latex que nos permita obtener nuestra gráfica en un pdf.


#+begin_src latex :results none :tangle fig.tex :exports both
  \documentclass{article}
  \usepackage{graphicx}
  \usepackage{color}
  \usepackage{amsmath}
  \usepackage{amssymb}
  \usepackage{xcolor}
  \usepackage[activeacute, spanish]{babel}
  \usepackage[utf8x]{inputenc}
  \usepackage[T1]{fontenc}

  \definecolor{mWhite}{rgb}{1.0, 1.0, 1.0}
  \newcommand\white[1]{\textcolor{mWhite}{#1}}
  \spanishdecimal{.}
  \renewcommand{\v}[1]{{\boldsymbol #1}}
  \newcommand{\PROM}[1]{\left\langle #1\right\rangle}
  \pagestyle{empty}
  
  \begin{document}
  \input{junio.tex}
  \end{document}  
#+end_src

* Script gp2pdf

#+begin_src shell :tangle gp2pdf.sh :results none :exports both
  #!/bin/bash
#--------------------------------------------------------------------
# SCRIPT: 	GP2PDF.SH
# AUTHOR: 	ISAIAS MORENO
# DATE: 	28-Mar-2013
# REV:		2.0
# 
# PLATFORM: Linux
## 
#--------------------------------------------------------------------
name=`basename $1`
name=${name%.gp}

# Ejecuta comandos en Gnuplot
echo "load '${name}.gp'
set terminal epsl col sol lw 2
set output '${name}.tex'
rep" | gnuplot

# Genera el archivo fig.tex
echo '\documentclass{article}' > fig.tex
echo '\usepackage[activeacute, spanish]{babel}' >> fig.tex
echo '\usepackage[utf8x]{inputenc}' >> fig.tex
echo '\usepackage[T1]{fontenc}' >> fig.tex
echo '\usepackage{graphicx}' >> fig.tex
echo '\usepackage{color}' >> fig.tex
echo '\usepackage{amsmath}' >> fig.tex
echo '\usepackage{amssymb}' >> fig.tex
echo '\usepackage{xcolor}' >> fig.tex
echo '' >> fig.tex
echo '\definecolor{mWhite}{rgb}{1.0, 1.0, 1.0}' >> fig.tex
echo '\newcommand\white[1]{\textcolor{mWhite}{#1}}' >> fig.tex
echo '\renewcommand{\v}[1]{{\boldsymbol #1}}'  >> fig.tex
echo '\spanishdecimal{.}' >> fig.tex
echo '\newcommand{\PROM}[1]{\left\langle #1\right\rangle}' >> fig.tex
echo '\pagestyle{empty}' >> fig.tex
echo '' >> fig.tex
echo '\begin{document}' >> fig.tex
echo '\input{'$name'.tex}' >> fig.tex
echo '' >> fig.tex
echo '\end{document}' >> fig.tex

## Latex to Pdf
latex fig
dvips -E fig.dvi -o fig.eps
epstopdf fig.eps -o "$name".pdf
rm "$name".eps

## Recorte del pdf
pdfcrop --margins "10 10 10 0" "$name".pdf
mv "$name"-crop.pdf "$name".pdf

rm "$name".tex
rm fig.aux fig.dvi fig.tex fig.log
rm fig.pdf fig.eps
##okular "$name".pdf &
#######################################################################
#+end_src


Agregar permiso de ejecucuón al archivo =gp2pdf.sh=

#+begin_src shell :exports both
  chmod +x gp2pdf.sh
#+end_src

Usar el archivo =gp2pdf.sh=

#+begin_src shell :results none :exports both
  #./gp2pdf.sh junio.gp
#+end_src


** Agregar el script gp2pdf al $PATH

Un script del shell puede agregarse a la variable =$PATH=, al colocar la sigiente linea de texto en el archivo =.bashrc=, =.zshrc= o =.profile= (macOS).

#+begin_src shell :exports both 
export PATH=$PATH:/home/isaias/Documents/script  
#+end_src

Ahora podemos hacer uso del script =gp2pdf= desde cualquier carpeta.

* Editor de texto

El editor de texto que recomiendo es =emacs=, pero pueden usar con el que se sientan más comodos.

A partir de ahora, únicamente escribiremos scripts de gnuplot.

* Comandos básicos de Gnuplot

** Etiquetas esenciales

#+begin_src gnuplot :file "./fig/title.png" :results file graphics :exports both
  reset
  set title 'Función sin(x)'
  set xlabel 'Eje X'
  set ylabel 'Eje y'
  set grid

  f(x) = sin(x)

  plot [0:2*pi] f(x)
#+end_src

#+RESULTS:
[[file:./fig/title.png]]
** Etiquetas en los ejes (xtics/ytics)

#+begin_src gnuplot :file "./fig/xtics.png" :results file graphics :exports both
  reset
  set title 'Función sin(x)'
  set xlabel 'Eje X'
  set ylabel 'Eje y'

  f(x) = sin(x)
  set xtics ("pi/2" pi/2, "pi" pi, "3pi/2" 3*pi/2, "2pi" 2*pi)
  set ytics 0.4
  set grid

  plot [0:2*pi] f(x)
#+end_src

#+RESULTS:
[[file:./fig/xtics.png]]

Es posible remover las etiquetas de los ejes utilizando =unset xtics= o =unset ytics=.


** Estilos


*** Test

#+begin_src gnuplot :file "./fig/test.png" :results file graphics :exports both
  test
#+end_src

#+RESULTS:
[[file:./fig/test.png]]


*** Obtención de datos a partir de una función

#+begin_src gnuplot
  set table './data/funciones.dat'

  f(x) = sin(x)
  g(x) = cos(x)

  plot [0:2*pi] f(x) , g(x)
  
  unset table
#+end_src

#+RESULTS:

*** Lineas y puntos

#+begin_src gnuplot :file "./fig/estilos.png" :results file graphics :exports both
  reset
  set key left bottom

  set xlabel 'Eje X'
  set ylabel 'Eje y'

  file='./data/funciones.dat'

  plot file index 0 using 1:2 with points title "sin(x)",\
       file index 1 using 1:2 with linespoints title "cos(x)"
#+end_src

#+RESULTS:
[[file:./fig/estilos.png]]

*** pointtype, pointsize, linewidth

#+begin_src gnuplot :file "./fig/estilos2.png" :results file graphics :exports both
  reset
  set key left bottom

  set xlabel 'Eje X'
  set ylabel 'Eje y'

  file='./data/funciones.dat'

  plot file index 0 using 1:2 with points pointtype 6 title "sin(x)",\
       file index 0 using 1:($2+0.1)  with lines linewidth 4 title "sin(x) + pi",\
       file index 1 using 1:2 with points pointtype 4 pointsize 0.1 title "cos(x)"
#+end_src

#+RESULTS:
[[file:./fig/estilos2.png]]

*** range

#+begin_src gnuplot :file "./fig/estilos3.png" :results file graphics :exports both
  reset
  #set key left bottom

  set xlabel 'y'
  set ylabel 'f(x)'
  set xrange [1:2]
  set yrange [0.6:1.3]

  file='./data/funciones.dat'
  
  plot file index 0 using 1:2 with linespoints title "sin(x)"
#+end_src

#+RESULTS:
[[file:./fig/estilos3.png]]



*** dashtype

Es posible definir tu propio patrón usando una cadena con los siguientes caracteres:

- "-":segmento sólido
- ".":punto
- "_":espacio en blanco

#+begin_src gnuplot :file "./fig/estilos4.png" :results file graphics :exports both
  reset
  set key left bottom

  set xlabel 'Eje X'
  set ylabel 'Eje y'

  file='./data/funciones.dat'

  plot file index 0 using 1:2 with lines dashtype 2  title "sin(x)",\
       file index 1 using 1:2 with lines dashtype "-.._"  title "cos(x)"      
#+end_src

#+RESULTS:
[[file:./fig/estilos4.png]]


*** Formato


#+begin_src gnuplot :file "./fig/estilos4.png" :results file graphics :exports both
  reset
  set key left bottom

  set format y "%.1f"
  set format x "%.1f"

  set xlabel 'x'
  set ylabel 'f(x)'

  file='./data/funciones.dat'

  plot file index 0 using 1:2 with lines title "sin(x)",\
       file index 1 using 1:2 with lines title "cos(x)"      
#+end_src

#+RESULTS:
[[file:./fig/estilos4.png]]

Ver [[https://gnuplot.sourceforge.net/docs_4.2/node184.html][Format specifiers]]

*** Más estilos

- =set datafile separator ','=, archivo separado por comas
- =set size square=, gráfica cuadrada
- =set size ratio 1.5=, relación de tamaño de los ejes.
- =unset key=, elimina las etiquetas de titulos
- =set key=
  - set key left top, mover el titulo a la izquierda (left, right, center) y hacia arriba (top, bottom, center).
  - set key out horiz, mover las etiquetas fuera de la caja en horizontal (horiz, vert)
- =unset border=, borra los ejes coordenados
- =set zeroaxis=, coloca los ejes coordenados
- =unset colorbox=, borra la caja de colores


No es necesario

#+begin_src gnuplot :results none
  plot file u 1:2 w l t ""
#+end_src


- u, using
- w, with
- l, lines
- p, points
- pt, point type
- ps, point size
- lw, line width


** Mover ejes al origen

#+begin_src gnuplot :file "./fig/origen.png" :results file graphics :exports both
    # Establecer los ejes en el origen
  reset
  set zeroaxis lt 1 lc 8 lw 2
  set xzeroaxis lt 1 lc 8 lw 2
  set xtics axis
  set ytics axis
  unset border

  # Posicionar las etiquetas de los ejes en el origen
  set label 'x' at 4.5, -0.5 
  set label 'y' at 0.5, 4.5 rotate by 0 #

  #set xtics offset 0.5,0
  #set ytics offset 0.0,0.5


  # Rango de visualización (ajusta según necesites)
  set xrange [-5:5]
  set yrange [-5:5]

  # Dibujar una cuadrícula opcional
  set grid

  # Dibujar algo como ejemplo (puedes eliminarlo)
  plot sin(x) with lines title 'sin(x)'  
#+end_src

#+RESULTS:
[[file:./fig/origen.png]]


#+begin_src gnuplot :file "./fig/origen2.png" :results file graphics :exports both
    # Establecer los ejes en el origen
  reset
  set zeroaxis lt 1 lc 8 lw 2
  set xzeroaxis lt 1 lc 8 lw 2
  set xtics axis
  set ytics axis
  unset border

  # Posicionar las etiquetas de los ejes en el origen
  set label 'x' at 4.7, -0.25 
  set label 'y' at 0.3, 1.3 rotate by 0 #

  set xtics ("pi/2" pi/2, "pi" pi, "3pi/2" 3*pi/2, "2pi" 2*pi)
  set ytics ("-pi/4" -pi/4, "pi/4" pi/4, "pi/2" pi/2)

  # Rango de visualización (ajusta según necesites)
  set xrange [-1:7]
  set yrange [-1:1.6]

  # Dibujar una cuadrícula opcional
  set grid

  # Dibujar algo como ejemplo (puedes eliminarlo)
  plot sin(x) with lines title 'sin(x)'  
#+end_src

#+RESULTS:
[[file:./fig/origen2.png]]



#+begin_src gnuplot :file "./fig/origen3.png" :results file graphics :exports both
    # Establecer los ejes en el origen
  reset
  set zeroaxis lt 1 lc 8 lw 2
  set xzeroaxis lt 1 lc 8 lw 2
  set xtics axis
  set ytics axis
  unset border

  # Posicionar las etiquetas de los ejes en el origen
  set label 'x' at 6.8, -0.25 
  set label 'y' at 0.3, 1.5 rotate by 0 #

  set xtics ('$\dfrac{\pi}{2}$'pi/2, '$\pi$'pi, '$\dfrac{3\pi}{2}$' 3*pi/2, '$2\pi$'2*pi)
  set ytics ('$-\dfrac{\pi}{4}$'-pi/4, '$\dfrac{\pi}{4}$'pi/4, '$\dfrac{\pi}{2}$'pi/2)

  # Rango de visualización (ajusta según necesites)
  set xrange [-1:7]
  set yrange [-1:1.6]

  # Dibujar una cuadrícula opcional
  set grid

  # Dibujar algo como ejemplo (puedes eliminarlo)
  plot sin(x) with lines title 'sin(x)'
  
#+end_src

#+RESULTS:
[[file:./fig/origen3.png]]

[[file:./fig/origen3.pdf]]


* Trabajar con fechas

#+begin_src gnuplot :file "./fig/fecha.png" :results file graphics :exports both
  reset

  set xdata time
  set timefmt "%Y-%m-%d"
  set format x "%b"

  set title ""
  set ylabel "sigma [cm]"
  set xlabel ""

  file="./data/fecha.dat"

  plot file using 1:2 w lp ls 5 title 'x', '' using 1:4 w lp ls 6 title 'z'
#+end_src

#+RESULTS:
[[file:./fig/fecha.png]]

Intervalo de interés

#+begin_src gnuplot :file "./fig/fecha2.png" :results file graphics :exports both
  reset

  set xdata time
  set timefmt "%Y-%m-%d"
  set format x "%d"

  set title ""
  set ylabel "sigma [cm]"
  set xlabel "Días de marzo"

  file="./data/fecha.dat"
  set xrange ["2017-03-01":"2017-04-01"]

  plot file using 1:2 w lp ls 5 title 'x', '' using 1:4 w lp ls 6 title 'z'
#+end_src

#+RESULTS:
[[file:./fig/fecha2.png]]
Para conocer los formatos completos de tiempo, revisar [[https://gnuplot.sourceforge.net/docs_4.2/node274.html][Timefmt]].

* Paletas de Colores

** Paletas de grises

#+begin_src shell :results output :exports both
  less greys.pal
#+end_src

#+RESULTS:
#+begin_example

## line styles
## lt -linetype
## lc -linecolor

set style line 1 lt 1 lc rgb '#252525' dt 2 # dark grey
set style line 2 lt 1 lc rgb '#525252' #
set style line 3 lt 1 lc rgb '#737373' # medium grey
set style line 4 lt 1 lc rgb '#969696' # 
set style line 5 lt 1 lc rgb '#BDBDBD' # light grey
set style line 6 lt 1 lc rgb '#D9D9D9' # 
set style line 7 lt 1 lc rgb '#F0F0F0' # 
set style line 8 lt 1 lc rgb '#FFFFFF' # white  

set style line 9 lt 1 dt 2 lc rgb '#252525' # dark grey
set style line 10 lt 1 dt 3 lc rgb '#252525' # dark grey  
set style line 11 lt 1 dt 4 lc rgb '#252525' # dark grey  
set style line 12 lt 1 dt 5 lc rgb '#252525' # dark grey  
set style line 13 lt 1 dt 6 lc rgb '#252525' # dark grey  
#+end_example



#+begin_src gnuplot :file "./fig/paletaGrises.png" :results file graphics :exports both
  reset
  set key left
  load "greys.pal"

  file="./data/parabola.dat"

  plot file using 1:2 with lines ls 1 lw 1 title "lt 1",\
       file using ($1+0.0):2 with lines ls 2 lw 1 title "lt 2",\
       file using ($1+1.0):2 with lines ls 3 lw 1 title "lt 3",\
       file using ($1+2.0):2 with lines ls 4 lw 1 title "lt 4",\
       file using ($1+3.0):2 with lines ls 5 lw 1 title "lt 5",\
       file using ($1+4.0):2 with lines ls 6 lw 1 title "lt 6",\
       file using ($1+5.0):2 with lines ls 7 lw 1 title "lt 7",\
       file using ($1+6.0):2 with lines ls 8 lw 1 title "lt 8"

  set output

#+end_src

#+RESULTS:
[[file:./fig/paletaGrises.png]]



** Paletas 

[[https://github.com/Gnuplotting/gnuplot-palettes][Paletas de gnuplot]], se descargan y guardan en una carpeta para después leerlas.

#+begin_src shell :result none
  set loadpath '/home/isaias/Documents/gnuplot/gnuplot-palettes'
  
  MATLAB = "defined (0  0.0 0.0 0.5, \
                   1  0.0 0.0 1.0, \
                   2  0.0 0.5 1.0, \
                   3  0.0 1.0 1.0, \
                   4  0.5 1.0 0.5, \
                   5  1.0 1.0 0.0, \
                   6  1.0 0.5 0.0, \
                   7  1.0 0.0 0.0, \
                   8  0.5 0.0 0.0 )"
#+end_src


#+begin_src gnuplot :file "./fig/paletas.png" :results file graphics :exports both
  reset
  load 'paired.pal'
  set key left bottom

  set format y "%.1f"
  set format x "%.1f"

  set xlabel 'x'
  set ylabel 'f(x)'

  file='./data/funciones.dat'

  plot file index 0 using 1:2 with lines ls 6 title "sin(x)",\
       file index 1 using 1:2 with lines ls 7 title "cos(x)"      
#+end_src

#+RESULTS:
[[file:./fig/paletas.png]]


* Tipos de gráficas

** Scatterplot (Dispersión)

#+begin_src gnuplot :file "./fig/plotSca.png" :results file graphics :exports both
  reset

  set xlabel 'x'
  set ylabel 'f(x)'
  set grid
  file='./data/scatterplot.dat'

  set format y "%.1tx10^{%+03T}"
  plot file with points title ""

#+end_src

#+RESULTS:
[[file:./fig/plotSca.png]]
Podemos utilizar otro formato.

#+begin_src gnuplot :file "./fig/plotSca2.png" :results file graphics :exports both
  reset

  set xlabel 'x'
  set ylabel 'f(x) \times 10^{-4}'
  set grid
  file='./data/scatterplot.dat'

  set format y "%.1t"
  plot file with points title ""
#+end_src

#+RESULTS:
[[file:./fig/plotSca2.png]]

Gráficaremos varios archivos y realizaremos operaciones 


#+begin_src gnuplot :file "./fig/plotSca3.png" :results file graphics :exports both
reset
load 'rdylbu.pal'
set datafile separator ','

# Pesos a dollar
dolar  = 17.80        # dolar del año 2023
factor = 0.8240892    # factor de conversión de dolar del 2023 al 2018
p2d(x) = (factor * x/dolar)/1000  # pesos/MWh -> dolar del 2018/kWh 

set xdata time
set timefmt "%Y-%m-%d"
set format x "%b"
set ytics format "%.2f"

set xrange ['2023-01-01':'2023-12-31']
set ylabel 'Marginal prices [\$/kWh]'
set grid

plot './data/preciosBCSday.csv' using 1:(p2d($3)) with lines ls 2 title 'BCS',\
     './data/preciosBCAday.csv' using 1:(p2d($3)) with lines ls 3 title 'BCA',\
     './data/preciosSINday.csv' using 1:(p2d($3)) with lines ls 7 title 'SIN'
#+end_src

#+RESULTS:
[[file:./fig/plotSca3.png]]

** Boxes

#+begin_src gnuplot :file "./fig/boxes.png" :results file graphics :exports both
  reset
  load "rdylbu.pal"
  set datafile separator ','

  set style fill solid 0.6

  set xtics ("Jan" 0, "Feb" 1, "Mar" 2, "Apr" 3, "May" 4, "Jun" 5, "Jul" 6, "Aug" 7, "Sep" 8, "Oct" 9, "Nov" 10, "Dec" 11)

  set ylabel 'DNI kWh/m$^2$/day'
  set yrange [1:10]
  set xrange [0.5:12.5]

  file="./data/DNIsam.csv"

  plot file using ($2/3.6) with histogram ls 2 title ''
#+end_src

#+RESULTS:
[[file:./fig/boxes.png]]


#+begin_src gnuplot :file "./fig/boxes1a.png" :results file graphics :exports both
  reset
  load 'dark2.pal'
  set datafile separator ','

  #set boxwidth 0.9 relative
  set style data histogram
  set style fill solid 0.3

  set xtics ("Jan" 1, "Feb" 2, "Mar" 3, "Apr" 4, "May" 5, "Jun" 6, "Jul" 7, "Aug" 8, "Sep" 9, "Oct" 10, "Nov" 11, "Dec" 12) #rotate by 45 offset -1.5, -1.2

  set ylabel 'DNI kWh/m$^2$/day'
  set yrange [0:10]
  set xrange [0:13]
  set grid

  plot './data/DNIsam.csv' using ($2/3.6) ls 1 title 'BCS',\
       './data/DNIsamHermosillo.csv' using ($2/3.6) ls 2 title 'Hermosillo',

#+end_src

#+RESULTS:
[[file:./fig/boxes1a.png]]


Si tienes una serie de datos que quieres mostrar en un histograma


#+begin_src gnuplot :file "./fig/boxes1.png" :results file graphics :exports both
reset
load 'spectral.pal'

file="./data/combinacionl.dat"

# Each bar is half the (visual) width of its x-range.
set boxwidth 0.02 absolute
set style fill solid 1.0 border -1

bin_width = 0.02;
bin_number(x) = floor(x/bin_width)
rounded(x) = bin_width * ( bin_number(x)+0.5 )

set xlabel '$\gamma_e$ [mrad]'
set ylabel "Frequency"

plot file using (rounded($26)):(N) smooth frequency with boxes ls 1 title ""
#+end_src

#+RESULTS:
[[file:./fig/boxes1.png]]


#+begin_src gnuplot :file "./fig/boxes2.png" :results file graphics :exports both
  reset
  load 'spectral.pal'

  file="./data/combinacionl.dat"
  
  stats file using 26 name "X" nooutput
  N=1.0/X_records
  ## X_records es el n'umero de puntos 
  titulo = sprintf("N = %d", X_records)

  # Each bar is half the (visual) width of its x-range.
  set boxwidth 0.02 absolute
  set style fill solid 1.0 border -1
  #set style fill solid 1.0 noborder

  bin_width = 0.02;
  bin_number(x) = floor(x/bin_width)
  rounded(x) = bin_width * ( bin_number(x) + 0.5 )

  set xlabel '$\gamma_e$ [mrad]'
  set ylabel "Frequency"

  plot file using (rounded($26)):N smooth frequency with boxes ls 1 t titulo
#+end_src

#+RESULTS:
[[file:./fig/boxes2.png]]



** yerrorbars

3 columnas: x y ydelta
4 columnas: x y ydelta xdelta # boxwidth !=-2
4 columnas: x y ylow yhigh    # boxwidth ==-2
5 columnas: x y ylow yhigh xdelta

El siguiente  conjunto de datos es la posición del sol comparado con el error.

#+begin_src gnuplot :file "./fig/boxerrorbars.png" :results file graphics :exports both
  reset
  load "set2.pal"

  file="./data/step2112.dat"

  set xlabel '$\gamma_s$'
  set ylabel '$\alpha$'

  set yrange [4.5:4.8]
  set xrange [-62.55:-62.45]

  set xtics 0.02

  d(x1,y1,x2,y2)= sqrt( (x2-x1)**2 + (y2-y1)**2 )

  plot file using 2:1:(d($1,$2,$3,$4)) with yerrorbars ls 1 title ""
#+end_src

#+RESULTS:
[[file:./fig/boxerrorbars.png]]


** Candlesticks

Se requiere un archivo previamente estructurado

"Grupo" "Min"	"Q1"	"Med"	"Q3"	"Max"

#+caption: Caja
#+attr_latex: :width 400px
#+label: fig:caja
[[file:./fig/normal.png]]



#+begin_src gnuplot :file "./fig/candlesticks.png" :results file graphics :exports both
  reset
  set key left
  load 'paired.pal'
  set boxwidth 1.5 absolute
  set style fill solid 0.5

  set title ""
  set xlabel "Número de facetas"
  set ylabel "$\\sigma_x$ [cm]"
  unset grid

  # # Data columns : X Min 1stQuartile Median 3rdQuartile Max
  set xrange [0:35]
  set yrange [0:170]

  file="./data/AHallBoxX.dat"
  
  plot file using 1:3:2:6:5 with candlesticks ls 1 title "$x_a$" whiskerbars
       
#+end_src

#+RESULTS:
[[file:./fig/candlesticks.png]]

Nos interesa indicar únicamente las facetas de estudio. Vamos a utilizar las etiquetas de la primera columna para indicarla

#+begin_src gnuplot :file "./fig/candlesticks2.png" :results file graphics :exports both
  reset
  set key left
  load 'paired.pal'
  set boxwidth 1.5 absolute
  set style fill solid 0.5

  set title ""
  set xlabel "Número de facetas"
  set ylabel "$\\sigma_x$ [cm]"
  unset grid

  # # Data columns : X Min 1stQuartile Median 3rdQuartile Max
  set xrange [0:35]
  set yrange [0:170]

  file="./data/AHallBoxX.dat"

  plot file using 1:3:2:6:5 with candlesticks ls 1 title "$x_a$" whiskerbars,\
       file using 1:4:4:4:4 with candlesticks ls 2,\
       file using 1:3:xticlabels(1) linetype -2 notitle
       
#+end_src

#+RESULTS:
[[file:./fig/candlesticks2.png]]



#+begin_src gnuplot :file "./fig/candlesticks3.png" :results file graphics :exports both
  reset
  set key autotitle columnhead
  load 'set2.pal'
  set key autotitle left
  set boxwidth 0.3 absolute
  set style fill solid 0.5

  #set xtics rotate by 25 offset -2.0,-0.7

  set title ""
  set xlabel ""
  set ylabel "$\\sigma_x$ [cm]"
  unset grid

  # # Data columns : X Min 1stQuartile Median 3rdQuartile Max
  set xrange [0:35]
  set yrange [0:320]

  file1="./data/AHallBoxX.dat"
  file2="./data/BHallBoxX.dat"
  file3="./data/CHallBoxX.dat"

  plot file1 using 1:3:xticlabels(1) linetype -3 notitle,\
       file1 using ($1-0.6):3:2:6:5 with candlesticks ls 5 title "$x_a$" whiskerbars,\
       file1 using ($1-0.6):4:4:4:4 with candlesticks ls 5,\
       file2 using 1:3:2:6:5 with candlesticks ls 6 title "$x_b$" whiskerbars,\
       file2 using 1:4:4:4:4 with candlesticks ls 6,\
       file3 using ($1+0.6):3:2:6:5 with candlesticks ls 7 title "$x_c$" whiskerbars,\
       file3 using ($1+0.6):4:4:4:4 with candlesticks ls 7
#+end_src

#+RESULTS:
[[file:./fig/candlesticks3.png]]


** Boxplot

Dato univariable

plot data using (start_x):(y-value):(boxwidth):(category) with boxplo

#+begin_src gnuplot :file "./fig/boxeplot.png" :results file graphics :exports both
  reset
  load 'spectral.pal'
  file="./data/combinacionl.dat"
  unset key

  set xtics nomirror
  set ytics nomirror
  set style fill solid 0.3
  set border 2                      ## Solo deja el borde de la izquierda

  set style boxplot nooutliers

  plot file using (1.0):26:xticlabel('A') with boxplot ls 1
#+end_src

#+RESULTS:
[[file:./fig/boxeplot.png]]


#+begin_src gnuplot :file "./fig/boxeplot2.png" :results file graphics :exports both
  reset
  load 'spectral.pal'
  file="./data/combinacionl.dat"

  unset key
  set border 2
  set xtics nomirror
  set ytics nomirror
  set style fill solid 0.3
  set style boxplot nooutliers

  plot file using (1.0):26:(0):2 with boxplot ls 1
#+end_src

#+RESULTS:
[[file:./fig/boxeplot2.png]]




** Matrices

#+begin_src gnuplot :file "./fig/matrices.png" :results file graphics :exports both
reset
load 'rdylbu.pal'
set palette negative

set pm3d implicit at b
set view map
unset surface

set xlabel ""
set ylabel "Hora del día"
set cblabel "Potencia de Generación [MW]" rot

set xrange [0:365]
set yrange [0:23]
set cbrange [0:50]
set ytics 2
unset border
set grid

set xtics ("Ene" 15, "Feb" 45, "Mar" 75, "Abr" 105, "May" 135, "Jun" 165, "Jul" 195, "Ago" 225, "Sep" 255, "Oct" 285, "Nov" 315, "Dic" 345) #rotate by 45 offset -1.5, -1.2


splot "./data/gen.dat" matrix using 2:1:($3/1000) notitle 
#+end_src

#+RESULTS:
[[file:./fig/matrices.png]]
Si tenemos una imagen

[[file:./fig/F2020-01-28-m_12-34-18_np.bmp]]

Transformanmos bmp2matrix

#+begin_src gnuplot :file "./fig/matrix2.png" :results file graphics :exports both
  reset
  set palette gray

  set xlabel "$x$ [m]"
  set ylabel "$z$ [m]"
  set cblabel "Escala de Grises" rot offset 1

  lrec= 2.44
  hrec= 2.44
  midx = lrec/2
  midy = hrec/2
  dx = 0.0081354
  dy = 0.00762693

  file='./data/F2020-01-28-m_12-34-18_np.dat'
  
  plot file matrix using (-midx+dx*$1):(-midy+dy*$2):3 with image notitle
#+end_src

#+RESULTS:
[[file:./fig/matrix2.png]]






** Parallelaxes

Por revisar

** Labels

Por revisar

** Spider

Por revisar


* Regresión lineal (fit)


#+begin_src gnuplot :file "./fig/fit.png" :results file graphics :exports both
  reset
  f(x) = A0*exp(-x/tau)
  fit f(x) './data/fit.dat' u 1:2 via A0, tau

  set xlabel "x"
  set ylabel "f(x)"

  file="./data/fit.dat"
  plot file with p, f(x)
#+end_src

#+RESULTS:
[[file:./fig/fit.png]]

* Filtar datos (awk)

Algunos contratiempo que pueden ocurrir son debido a que no se 

#+begin_src gnuplot :file "./fig/awk.png" :results file graphics :exports both
  reset

  set key left
  set size ratio -1
  set xlabel 'X/d'
  set ylabel 'Z/d'

  file = './data/derivaDays.dat'

  set xtics rotate by 90 right
  set grid
  set xrange [0.005:0.025]
  set yrange [-0.025:-0.005]

  plot file  using ($7/$6):($9/$6) with lines title ''
       
#+end_src

#+RESULTS:
[[file:./fig/awk.png]]



** AWK

Para la linea de comandos la sintaxis es:

#+begin_src sh :results verbatim :exports both
  awk 'instructions' files
#+end_src


#+begin_src sh :results output :exports both
  awk '{ print $1}' ./data/fecha.dat | head
#+end_src

#+RESULTS:
#+begin_example
#"fecha"
2017-01-07
2017-01-14
2017-01-21
2017-01-28
2017-02-07
2017-02-14
2017-02-21
2017-02-28
2017-03-07
#+end_example

#+begin_src sh :results output :exports both
  awk '/-01-/' ./data/fecha.dat
#+end_src

#+RESULTS:
: 2017-01-07	1.58996855314814	0	0.446965074449646	0.22405540697967
: 2017-01-14	1.37175591331486	0	0.435416239462083	0.200582580072576
: 2017-01-21	1.12224265265074	0	0.397931157801455	0.171419952164268
: 2017-01-28	0.834266144584568	0	0.349375860733812	0.138959866308386


#+begin_src sh :results output :exports both
  awk '/-01-/ { print $2 }' ./data/fecha.dat
#+end_src

#+RESULTS:
: 1.58996855314814
: 1.37175591331486
: 1.12224265265074
: 0.834266144584568

Cambiar el campo de separación

#+begin_src sh :results output :exports both
  awk -F, '{ print $3}' ./data/data.csv | head
#+end_src

#+RESULTS:
#+begin_example
"Wind"
7.4
8
12.6
11.5
14.3
14.9
8.6
13.8
20.1
#+end_example


#+begin_src sh :results verbatim :exports both
  awk 'condicion { accion }' file
#+end_src


Revisaremos el archivo =derivaDays.dat=.

#+begin_src sh :results verbatim :exports both
 head ./data/derivaDays.dat
#+end_src

#+RESULTS:
#+begin_example
id	Y-M-D h:m:s	cenit	acimut	d	p1	p2	p3	r
1	2024-06-21 07:00:00	71.9124	-107.3110	640.7995	11.1788	0.0000	-10.8975	15.6115
1	2024-06-21 07:05:00	70.8699	-106.8122	640.7995	11.0928	0.0000	-11.0623	15.6660
1	2024-06-21 07:10:00	69.8245	-106.3167	640.7995	11.0049	0.0000	-11.2230	15.7183
1	2024-06-21 07:15:00	68.7763	-105.8243	640.7995	10.9152	0.0000	-11.3798	15.7684
1	2024-06-21 07:20:00	67.7253	-105.3347	640.7995	10.8240	0.0000	-11.5326	15.8164
1	2024-06-21 07:25:00	66.6717	-104.8476	640.7995	10.7313	0.0000	-11.6813	15.8624
1	2024-06-21 07:30:00	65.6157	-104.3628	640.7995	10.6374	0.0000	-11.8260	15.9062
1	2024-06-21 07:35:00	64.5572	-103.8800	640.7995	10.5424	0.0000	-11.9666	15.9481
1	2024-06-21 07:40:00	63.4965	-103.3988	640.7995	10.4464	0.0000	-12.1032	15.9880
#+end_example

Vamos a filtar únicamente el día 21 de junio

#+begin_src sh :results verbatim :exports both
  awk '/2024-06-21/ { print }' ./data/derivaDays.dat | head
#+end_src

#+RESULTS:
#+begin_example
1	2024-06-21 07:00:00	71.9124	-107.3110	640.7995	11.1788	0.0000	-10.8975	15.6115
1	2024-06-21 07:05:00	70.8699	-106.8122	640.7995	11.0928	0.0000	-11.0623	15.6660
1	2024-06-21 07:10:00	69.8245	-106.3167	640.7995	11.0049	0.0000	-11.2230	15.7183
1	2024-06-21 07:15:00	68.7763	-105.8243	640.7995	10.9152	0.0000	-11.3798	15.7684
1	2024-06-21 07:20:00	67.7253	-105.3347	640.7995	10.8240	0.0000	-11.5326	15.8164
1	2024-06-21 07:25:00	66.6717	-104.8476	640.7995	10.7313	0.0000	-11.6813	15.8624
1	2024-06-21 07:30:00	65.6157	-104.3628	640.7995	10.6374	0.0000	-11.8260	15.9062
1	2024-06-21 07:35:00	64.5572	-103.8800	640.7995	10.5424	0.0000	-11.9666	15.9481
1	2024-06-21 07:40:00	63.4965	-103.3988	640.7995	10.4464	0.0000	-12.1032	15.9880
1	2024-06-21 07:45:00	62.4335	-102.9191	640.7995	10.3496	0.0000	-12.2357	16.0258
#+end_example




** Filtrando por día

#+begin_src gnuplot :file "./fig/awk1.png" :results file graphics :exports both
  reset

  set key left
  set size ratio -1
  set xlabel 'X/d'
  set ylabel 'Z/d'
  
  file = './data/derivaDays.dat'
  condition(y,m,d)=sprintf('/%d-%.2d-%.2d/', y,m,d)
  action=sprintf(" print ")
  cmd(y,m,d)= sprintf("<awk '%s {%s}' %s", condition(y,m,d), action, file)

  set xtics rotate by 90 right
  set grid
  set xrange [0.005:0.025]
  set yrange [-0.025:-0.005]

  plot cmd(2024,6,21)  using ($7/$6):($9/$6) with lines title '21-jun',\
       cmd(2024,8,6)   using ($7/$6):($9/$6) with lines title '06-ago',\
       cmd(2024,8,26)  using ($7/$6):($9/$6) with lines title '26-ago',\
       cmd(2024,9,13)  using ($7/$6):($9/$6) with lines title '13-sep',\
       cmd(2024,9,29)  using ($7/$6):($9/$6) with lines title '29-sep',\
       cmd(2024,10,17) using ($7/$6):($9/$6) with lines title '17-oct',\
       cmd(2024,11,6)  using ($7/$6):($9/$6) with lines title '06-nov',\
       cmd(2024,12,21) using ($7/$6):($9/$6) with lines title '21-dic'
#+end_src

#+RESULTS:
[[file:./fig/awk1.png]]





* Ciclos (loop)

#+begin_src text :results none :exports both
  plot for [<variable>=<start>:<end>{:<incremento>}]
  plot for [<variable> in "string pf words"]
#+end_src



#+begin_src gnuplot :file "./fig/loop.png" :results file graphics :exports both
  reset
  plot for [j=1:3] sin(j*x)
#+end_src

#+RESULTS:
[[file:./fig/loop.png]]


** word/words

La función word("string", n) regresa el n-ésimo valor de una lista
La función words(list), regresa el numero de elementos de una lista

#+begin_src gnuplot :file "./fig/loop2.png" :results file graphics :exports both
  reset
  set datafile separator ','

  set xdata time
  set timefmt "%Y-%m-%d"
  set format x "%b"
  #set ytics format "%.2f"

  set ylabel 'Precios [\$/MWe]'

  list="preciosBCAday preciosBCSday preciosSINday"
  item(n)=word(list,n)

  plot for [j=1:words(list)] "./data/".item(j).".csv" using 1:3 with lines ls j title item(j)
#+end_src

#+RESULTS:
[[file:./fig/loop2.png]]

** list

#+begin_src gnuplot :file "./fig/loop3.png" :results file graphics :exports both
  reset
  set datafile separator ','

  set xdata time
  set timefmt "%Y-%m-%d"
  set format x "%b"
  #set ytics format "%.2f"

  set ylabel 'Precios [\$/MWe]'

  list="preciosBCAday preciosBCSday preciosSINday"

  plot for [j in list] "./data/".j.".csv" using 1:3 with lines title j
#+end_src

#+RESULTS:
[[file:./fig/loop3.png]]


** sprintf

#+begin_src gnuplot :file "./fig/loop4.png" :results file graphics :exports both
  reset

  file(n)=sprintf("./data/lineas/StreamLines%d00.txt",n)

  plot for [i=2:8:2] file(i) using 1:3 with lines title file(i)
#+end_src

#+RESULTS:
[[file:./fig/loop4.png]]

#+begin_src gnuplot :file "./fig/loop5.png" :results file graphics :exports both
  reset

  file(n)=sprintf("StreamLines%d00",n)

  plot for [i=2:8:2] "./data/lineas/".file(i).".txt" using 1:3 with lines title file(i)
#+end_src

#+RESULTS:
[[file:./fig/loop5.png]]


* Ejes secundarios


#+begin_src gnuplot :file "./fig/eje.png" :results file graphics :exports both
  reset
  load "set2.pal"
  set y2tics
  #set y2range [-6:6]
  set ytics nomirror

  set xlabel "x"
  set ylabel "f(x)"
  set y2label "g(x)"
  set grid

  f(x) = sin(1/x)
  g(x) = cos(1/x)

  plot f(x) axis x1y1 ls 1, g(x) axis x1y2 ls 2 
#+end_src

#+RESULTS:
[[file:./fig/eje.png]]

#+begin_src gnuplot :file "./fig/eje1.png" :results file graphics :exports both
  reset
  set key left
  file="./data/almacenamiento.dat"

  set y2tics
  #set y2nomirror
  
  set xlabel "Tiempo [h]"
  set ylabel "Q [MJ]"
  set y2label "T_s [C]"


  plot file using 1:2 with linespoints axes x1y1 title "Q_c",\
       file using 1:3 with linespoints axes x1y1 title "Q_l",\
       file using 1:5 with linespoints axes x1y1 title "Q_p",\
       file using 1:4 with linespoints axes x1y2 title "T_s"
#+end_src

#+RESULTS:
[[file:./fig/eje1.png]]



#+begin_src gnuplot :file "./fig/eje2.png" :results file graphics :exports both
  reset
  load 'dark2.pal'
  set datafile separator ','

  data = './data/25.038_-111.660_25.0384_-111.66_psm3-tmy_60_tmy.csv'
  stats data using 6 skip 3

  bins = 50
  max = STATS_max
  min = STATS_min
  width=(max-min)/bins
  hist(x,width)=width*floor(x/width)+width/2.0
  set boxwidth width*0.9
  set style fill solid 0.5 # fill style
  set grid

  set xtics 0,100,1000
  set y2tics 0,0.1,1
  set ytics nomirror
  set xrange [0:1020]

  set xlabel  'DNI [W/m$^2$]'
  set ylabel  '\# de datos'
  set y2label 'CFD'


  condition="FNR>3"
  action="if($6 > 0) print "
  cmd = sprintf("<awk -F ',' '%s {%s}' %s", condition, action, data)

  plot cmd using (hist($6,width)):(1.0) skip 3 smooth freq w boxes ls 6 notitle,\
       './data/DNI_cfd.csv' using 1:2 with lines ls 8 lw 2 notitle axis x1y2
#+end_src

#+RESULTS:
[[file:./fig/eje2.png]]


* Multiplot

Multiplot te permite combinar varios gráficos en una misma figura.

#+begin_src gnuplot :results none
set multiplot  
  # Aquí van los gráficos individuales  
unset multiplot    
#+end_src

- Origen (origin):
  (0,0): Esquina inferior izquierda.
  (0.5,0.5): Centro de la figura.
- Tamaño (size):
  (1,1): Ocupa toda la figura.
  (0.5,0.5): Mitad del ancho/alto.

*margins*

- set lmargin: Margen izquierdo.
- set rmargin: Margen derecho.
- set tmargin: Margen superior (top).
- set bmargin: Margen inferior (bottom).

set margin <l/r/t/b> spacing <valor> 


** Disposición
#+begin_src gnuplot :file "./fig/multiplot0.png" :results file graphics :exports both
  reset
  set multiplot layout 2,2  # 2 filas, 2 columnas  
  plot sin(x)  
  plot cos(x)  
  plot tan(x)  
  plot x**2  
  unset multiplot  
#+end_src

#+RESULTS:
[[file:./fig/multiplot0.png]]

* posición manual

#+begin_src gnuplot :file "./fig/multiplot1.png" :results file graphics :exports both
  reset
  set multiplot  
  set origin 0.0, 0.5  # Esquina inferior izquierda (x,y en coordenadas normalizadas 0-1)  
  set size 0.5, 0.5    # Ancho=50%, Alto=50%  
  plot sin(x)  

  set origin 0.5, 0.5  
  set size 0.5, 0.5  
  plot cos(x)

  set origin 0.0, 0.0  
  set size 1.0, 0.5  
  plot tan(x)

  unset multiplot
#+end_src

#+RESULTS:
[[file:./fig/multiplot1.png]]






** Ejemplos

#+begin_src gnuplot :file "./fig/multiplot2.png" :results file graphics :exports both
reset
load "set2.pal"

set multiplot

file="./data/step2112.dat"

set xlabel '$\gamma_s$'
set ylabel '$\alpha$'

set yrange [3:45]
set xrange [-65:65]

x1=-62.6
x2=-62.1
y1=4.6
y2=5.6

#set object 1 rect from x1,y1 to x2,y2
set arrow from x1,y1 to -43,4.8 head filled
set arrow from x1,y1 to -43,15 head filled

plot file using 2:1 with linespoints ls 1 title "solar",\
     file using 4:3 with linespoints ls 2 title "step"

##############################################################################
#unset border
reset
load "set2.pal"
unset key
unset xtics
unset ytics
unset xlabel
unset ylabel

set size 0.3,0.3
set origin 0.21,0.12

set xrange [x1:x2]
set yrange [y1:y2]

plot file using 2:1 with linespoints ls 1 title "solar",\
     file using 4:3 with linespoints ls 2 title "step"

unset multiplot
#+end_src

#+RESULTS:
[[file:./fig/multiplot2.png]]



#+begin_src gnuplot :file "./fig/multiplot3.png" :results file graphics :exports both
  reset

  set xlab ""
  set ylab ""
  set grid

  set format x "%0.2f"
  set xrange [3.08:3.20]
  set xtics 3.08,0.02,3.20
  set ytics 0,100,900

  binwidth=0.001
  bin(x,width)=width*floor(x/width)

  ### Start multiplot (2x2 layout)
  set multiplot layout 2,2 rowsfirst

  # --- GRAPH a
  plot './data/pi_8000.dat' using (bin($2,binwidth)):(1.0) smooth freq with boxes t "N=8000"

  # --- GRAPH b
  plot './data/pi_16000.dat' using (bin($2,binwidth)):(1.0) smooth freq with boxes t "N=16000"

  # --- GRAPH c
  plot './data/pi_32000.dat' using (bin($2,binwidth)):(1.0) smooth freq with boxes t "N=32000"

  # --- GRAPH d
  plot './data/pi_64000.dat' using (bin($2,binwidth)):(1.0) smooth freq with boxes t "N=64000"

  unset multiplot
#+END_SRC

#+RESULTS:
[[file:./fig/multiplot3.png]]


** Ejemplo

#+begin_src gnuplot :file "./fig/multiplot4.png" :results file graphics :exports both
  reset
  load 'spectral.pal'
  set datafile columnheader
  set datafile separator ","

  set multiplot layout 3,3\
      margins 0.1,0.95,0.1,0.95 \
      spacing 0.0,0.0

  set format y "%.1f"
  set format x "%.1f"

  f='./data/outputA.dat'
  g='./data/driftRealA.dat'

  condition(y,m,d)=sprintf('/%d-%.2d-%.2d/', y,m,d)
  action=sprintf(" print ")
  cmd(y,m,d, file)= sprintf("<awk '%s {%s}' %s", condition(y,m,d), action, file)

  #set xtics rotate by 90 right
  set xrange [6.5:9.5]
  set x2range [6.5:9.5]
  set yrange [2.0:6.5]
  set y2range [2.0:6.5]
  set format y2 "%0.1f"
  set y2tics 2,1,6 

  ## 01
  unset y2tics
  unset xtics
  unset ytics
  set xlabel ''
  set ylabel ''

  set ytics 1 nomirror
  plot cmd(2024,6,21,f)  using 7:9 with lines  ls 1 title '21 Jun',\
       cmd(2024,6,21,g)  using 7:9 with lines  ls 1 dt 2 title '$\kappa=$'


  set x2tics
  unset ytics
  plot cmd(2024,8,3,f)   using 7:9 with lines  ls 2 title '03 Aug',\
       cmd(2024,8,3,g)   using 7:9 with lines  ls 2 dt 2 title '$\kappa$'


  unset x2tics
  plot cmd(2024,8,21,f)  using 7:9 with lines  ls 3  title '21 Aug',\
       cmd(2024,8,21,g)  using 7:9 with lines  ls 3 dt 2 title '$\kappa$'

  set ylabel 'z [m]'
  unset ytics

  plot cmd(2024,9,6,f)   using 7:9 with lines  ls 4 title '06 Sep',\
       cmd(2024,9,6,g)   using 7:9 with lines  ls 4 dt 2 title '$\kappa$'

  unset ytics
  set ylabel ''
  plot cmd(2024,9,21,f)  using 7:9 with lines  ls 5 title '21 Sep',\
       cmd(2024,9,21,g)  using 7:9 with lines  ls 5 dt 2 title '$\kappa$'

  set y2tics
  plot cmd(2024,10,6,f)  using 7:9 with lines  ls 6 title '06 Oct',\
       cmd(2024,10,6,g)  using 7:9 with lines  ls 6 dt 2 title '$\kappa$'

  set xtics nomirror
  set ytics nomirror
  unset y2tics
  plot cmd(2024,10,21,f) using 7:9 with lines  ls 7 title '21 Nov',\
       cmd(2024,10,21,g) using 7:9 with lines  ls 7 dt 2 title '$\kappa$'

  set xlabel 'x [m]'
  unset ytics
  unset xtics
  plot cmd(2024,11,9,f)  using 7:9 with lines  ls 8 title '11 Nov',\
       cmd(2024,11,9,g)  using 7:9 with lines  ls 8 dt 2 title '$\kappa$'

  set xlabel ''
  set xtics nomirror
  plot cmd(2024,12,21,f) using 7:9 with lines  ls -1 title '21 Dec',\
       cmd(2024,12,21,g) using 7:9 with lines  ls 0 dt 2 title '$\kappa$'

  unset multiplot
#+end_src

#+RESULTS:
[[file:./fig/multiplot4.png]]


#+begin_src gnuplot :file "./fig/multiplot5.png" :results file graphics :exports both
  reset
  load 'rdylbu.pal'


  set multiplot layout 3,1\
      margins 0.1,0.99,0.1,0.98 \
      spacing 0.0,0.0

  ##############################################################################
  ## 01
  ##############################################################################
  reset
  #set palette @MATLAB
  #load 'ylgnbu.pal'
  load 'rdylbu.pal'
  set palette negative
  #set size square
  #set cbrange [0:40]

  set pm3d implicit at b
  set view map
  unset surface
  set colorbox user origin 0.90,0.75 size 0.01,0.2

  set xlabel ""
  set ylabel "Hour of day"
  set format cb "%.2f"
  #set cblabel "Electricity price [\\$/kWh]" rot

  set xrange [0:365]
  set yrange [1:23]
  set cbrange [0:0.40]
  set cbtics 0.10
  set ytics 4
  unset border

  #set xtics ("Jan" 1, "Feb" 31, "Mar" 61, "Apr" 91, "May" 121, "Jun" 151, "Jul" 181, "Aug" 211, "Sep" 241, "Oct" 271, "Nov" 301, "Dec" 331) #rotate by 45 offset -1.5, -1.2
  unset xtics

  splot "./data/precioM.dat" matrix using 2:1:3 notitle


  ##############################################################################
  ## 02
  ##############################################################################
  reset
  load 'rdylbu.pal'
  set key autotitle columnhead
  set key left
  set datafile separator ","

  set ylabel 'Electricity price [\$/kWh]' offset 0,-4.5,0
  set xrange [-0.5:11.8]
  #et xtics ("Jan" 1, "Feb" 2, "Mar" 3, "Apr" 4, "May" 5, "Jun" 6, "Jul" 7, "Aug" 8, "Sep" 9, "Oct" 10, "Nov" 11, "Dec" 12)
  #set xtics ('Ene' 0, 'Feb' 1, 'Mar' 2, 'Abr' 3, 'May' 4, 'Jun' 5, 'Jul' 6, 'Ago' 7, 'Sep' 8, 'Oct' 9, 'Nov' 10, 'Dic' 11)


  set format y "%.2f"
  data='./data/summary.dat'
  data2='./data/summaryF.dat'
  #set grid

  unset xtics
  set yrange [0.01:0.35]
  set ytics 0.1
  # mes,count,mean,std,min,25%,50%,75%,max
  plot data using ($0-0.15):6:5:9:8:($2/$2*0.2) with candlesticks ls 1 title 'Weekday'  whiskerbars,\
       '' using ($0-0.15):3:3:3:3 with candlesticks ls 1 notitle,\
       data2 using ($0+0.15):6:5:9:8:($2/$2*0.2) with candlesticks ls 7 title 'Weekend' whiskerbars,\
       '' using ($0+0.15):3:3:3:3 with candlesticks ls 7 notitle



  ##############################################################################
  ## 03
  ##############################################################################
  reset
  load 'rdylbu.pal'
  set key out horiz center bottom
  set xdata time
  set timefmt '%Y-%m-%d %H:%M:%S'
  set format x '%b'
  set format y "%.2f"

  data='./data/TODhora.dat'
  #set grid

  #set xlabel 'Hora'
  #set ylabel 'Electricity price [\$/kWh]'
  unset ylabel
  set yrange [0.01:0.45]
  set ytics 0.1
  set xrange ['2023-01-01 00:00:00':'2023-12-31 23:00:00']

  plot data using 2:4 with lines ls 7 dt 1 lw 0.1 title ''
  
  unset multiplot
#+end_src

#+RESULTS:
[[file:./fig/multiplot5.png]]


* Gráficas en 3D

** Spot

#+begin_src gnuplot :file "./fig/spot3d.png" :results file graphics :exports both
  reset
  set palette @MATLAB
  set dgrid3d 40,40
  set size square

  set xlabel "x" offset 1, -1
  set ylabel "z"
  set cblabel "Potencia \[kW/m$^2$\]" rot offset 1

  set xtics -1,0.4,1 offset 0.5,-0.5
  set ytics -1,0.4,1 offset 0.5,-0.5

  set pm3d
  #set pm3d interpolate 2,2
  a = 0.0496787*0.0485711

  splot "./data/xyz.dat" u 1:2:($3/a/1000) w pm3d title ""
#+end_src

#+RESULTS:
[[file:./fig/spot3d.png]]

#+begin_src gnuplot :file "./fig/spot2d.png" :results file graphics :exports both
  reset
  set palette @MATLAB
  set dgrid3d 40,40
  set size square
  #set cbrange [0:6]

  set pm3d implicit at b
  #set pm3d interpolate 2,2
  set view map
  unset surface

  set xlabel "x"
  set ylabel "z"
  set cblabel "Potencia \[kW/m$^2$\]" rot offset 1


  set xrange [-1:1]
  set yrange [-1:1]
  set xtics -1,0.4,1
  set ytics -1,0.4,1

  a = 0.0496787*0.0485711

  splot "./data/xyz.dat" u 1:2:($3/a/1000) w pm3d title ""
#+end_src

#+RESULTS:
[[file:./fig/spot2d.png]]

#+begin_src gnuplot :file "./fig/spotCon.png" :results file graphics :exports both
  reset
  set palette @MATLAB
  set dgrid3d 40,40
  set size square

  set view map
  unset surface

  set xlabel "x"
  set ylabel "z"

  set xrange [-1:1]
  set yrange [-1:1]
  set xtics -1,0.4,1
  set ytics -1,0.4,1

  # xr = -0.021387
  # yr = -0.01038
  # dr = 0.05
  # set arrow from xr,(yr-dr) to xr, (yr+dr) nohead lc rgb "red" front
  # set arrow from (xr-dr),yr to (xr+dr),yr nohead lc rgb "red" front
  # set object 1 ellipse at xr,yr size dr,dr front
  # set object 1 front lw 1 fc rgb "red" fs empty border

  a=0.0496787*0.0485711

  set contour base
  set cntrparam levels auto #discrete 10, 30, 50
  # set cntrparam levels discrete 10, 30, 50

  splot "./data/xyz.dat" u 1:2:($3/a/1000) w lines title ""      
#+end_src

#+RESULTS:
[[file:./fig/spotCon.png]]


* Dibujar en gnuplot



set object <id> rectangle from <x1>,<y1> to <x2>,<y2>
set object <id> ellipse at <x1>,<y1> size <r1>,<r2>


#+begin_src gnuplot :file "./fig/draft.png" :results file graphics :exports both
reset
set size square

set xrange [-1.1:1.1]
set yrange [-1.1:1.1]
set grid

set xzeroaxis lt 1 lc 8 lw 2
set yzeroaxis lt 1 lc 8 lw 2

set object 1 rectangle from -1,-1 to 1,1
set object 1 front lw 1 fillcolor rgb "#0000ff" fillstyle pattern 4

set object 2 ellipse at 0,0 size 2,2
set object 2 front lw 1 fillstyle transparent solid 0.1 fillcolor "red"

x=0.4
f(x)=sqrt(1-x**2)

set arrow from 0,0 to x,f(x) lw 3 lc "blue" head  # Flecha con punta
set arrow from x,0 to x,f(x) lw 3 dashtype 2 lc "blue" nohead 

set label "a" at x/2,-0.05
set label "b" at x+0.05,f(x)/2
set label '$c=\sqrt{a^2 + b^2 }$' at 0.25,-0.25

plot NaN notitle
#+end_src

#+RESULTS:
[[file:./fig/draft.png]]



#+begin_src gnuplot :file "./fig/draft1.png" :results file graphics :exports both
reset
load 'spectral.pal'
set datafile columnheader
set key left
#set size ratio -1
set xlabel 'x [m]'
set ylabel 'z [m]'

set format y "%.1f"
set format x "%.1f"

#set format y ""
#set format x ""

#set format x "%.3t"#; set ytic(12345)

f='./data/driftReal.dat'
condition(y,m,d)=sprintf('/%d-%.2d-%.2d/', y,m,d)
action=sprintf(" print ")
cmd(y,m,d, file)= sprintf("<awk '%s {%s}' %s", condition(y,m,d), action, file)

#set xtics rotate by 90 right
#set xrange [-2.0:-0.2]
#set yrange [-1.2:1.0]
set grid

#set title "Errores de deriva del 21 de Junio"


set label '$\omega=0^\circ$' at  8.32, 5.2362
set label '$\omega=30^\circ$' at 8.48, 5.6018


set object circle at first  8.2826, 5.2362 radius char 0.5 fillcolor rgb 'red' fillstyle solid noborder
set object circle at first 8.4402, 5.6018 radius char 0.5 fillcolor rgb 'red' fillstyle solid noborder

plot cmd(2024,9,30,f) using 8:10 with lines ls 1  title ''
#+end_src

#+RESULTS:
[[file:./fig/draft1.png]]


* Animación


#+begin_src gnuplot :results none :exports both
reset
set size ratio -1

date="2025-06-21"
system(sprintf("mkdir -p %s", date))

file(date)=sprintf("./data/rot_%s.dat", date)
condition=""
action(id)=sprintf("if($5 == %d ) print ", id)
cmd(id)= sprintf("<awk '%s {%s}' %s", condition, action(id), file(date))

file2(date)=sprintf("./data/poligono_%s.dat", date)
condition2=""
action2(id)=sprintf("if($4 == %d ) print ", id)
cmd2(id)= sprintf("<awk '%s {%s}' %s", condition2, action2(id), file2(date))

N=system(sprintf("awk 'END {print $5}' %s ",file(date)))
titulo(id)=system(sprintf("./title.sh %s %d", file(date), id))


## Tamaño del Fresnel
w=1.14
h=0.81

# Caja
xmin=-w/2
xmax=w/2
ymin=-h/2
ymax=h/2
set object 1 rect from xmin,ymin to xmax,ymax

set xrange[xmin-0.2:xmax+0.2]
set yrange[ymin-0.2:ymax+0.2]
set grid

set xlabel "y"
set ylabel "z"
set zlabel "x"

set style fill transparent solid 0.2

do for [i=1:N] {
    set terminal pngcairo size 800,600 enhanced font 'Verdana,10'
    set output sprintf("./%s/h%02d.png", date, i)

    set title titulo(i)
     plot cmd(i) using 2:3 with linespoints title "",\
 	 cmd2(i) using 1:2 with filledcurves  title "",\
	 cmd2(i) using 1:2 with linespoints title ""
     
     set output
}
#+end_src

#+begin_src shell
  png2video h
#+end_src


** Línea de corriente

Programa que resuelve el campo de velocidades
para un "estaNque" en reposo y en presencia
de una fuerza de Lorentz y además muestra las líneas
de corriente del sistema.


#+begin_src gnuplot :file "./fig/corriente.png" :results none :exports both
reset
load 'spectral.pal'

set xrange[-2:2]
set yrange[-2:2]
unset key
unset surface
set contour

set view map
set sty da l
set cntrparam levels auto 50

set term gif animate
set output "./fig/LineasCorriente.gif"

cmd(x)=sprintf("./data/lineas/StreamLines%d.txt",x)

do for [i=200:50000:200]{
    splot cmd(i)
    pause 0.1
}
#+end_src


** Campos

Programa que resuelve el campo de velocidades
para un flujo vertical con una oposición
de una fuerza de Lorentz. 


#+begin_src gnuplot :file "./fig/campo.png" :results none :exports both
  reset
  load 'spectral.pal'
  set size ratio 1.3
  unset key

  set xrange[-1:1]
  set yrange[-5:5]

  set term gif animate
  set output "./fig/ObstMag.gif"

  cmd(x)=sprintf("./data/campo/vel%d.txt",x)

  do for [i=200:100000:200]{
      plot cmd(i) using 1:2:(0.08*$3):(0.08*$4) with vector lt 3
      pause 0.1
  }
#+end_src



** Deriva

#+begin_src gnuplot :file "./fig/deriva.png" :results file graphics :exports both
reset
set palette @MATLAB
set size square
set cbrange [0:15]


set pm3d implicit at b
set view map
unset surface
set xlabel 'x (m)'
set ylabel 'z (m)'

lrec=8.000
hrec=8.000
midx = lrec/2
midy = hrec/2
dx = 0.05
dy = 0.05

set title "Date 15/11/2015 12.00"
splot "./data/deriva/D15M11_H12p00_Real.dat" matrix  using (-midx+dx*$1):(-midy+dy*$2):3 notitle
#+end_src

#+RESULTS:
[[file:./fig/deriva.png]]

ruta: /home/isaias/Documents/escuela/PhD/Code/Collado+Deriva/CampoDay_Correccion_V2/data/campo1511


#+begin_src gnuplot :results none :exports both
  reset
  set term post enh color eps "Times-Roman" 25

  #system("mkdir -p deriva")

  set palette @MATLAB
  set size square
  set cbrange [0:15]
  set xlabel 'x (m)'
  set ylabel 'z (m)'

  set pm3d implicit at b
  set view map
  unset surface

  lrec=8.000
  hrec=8.000
  midx = lrec/2
  midy = hrec/2
  dx = 0.05
  dy = 0.05

  filename(D,M,H,Min) = sprintf('./data/deriva/D%dM%d_H%dp%.2d_Real.dat', D,M,H,Min)
  title2(D,M,H,Min) = sprintf('Date %.2d/%.2d/2015 %.2d.%.2d', D,M,H,Min)

  i=0
  do for [H=7:17]{
      do for [Min=00:90:50]{
          set title title2(15,11,H,Min)
  	set output sprintf('./deriva/Real_%.2d.png', i)
  	splot filename(15,11,H,Min) matrix  using (-midx+dx*$1):(-midy+dy*$2):3 notitle
  	i=i+1
  }}
#+end_src


* Gráficas en tiempo real

#+begin_src gnuplot :file "./fig/label.png" :results file graphics :exports both

#+end_src


